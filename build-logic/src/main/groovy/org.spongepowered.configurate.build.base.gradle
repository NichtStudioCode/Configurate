import org.spongepowered.configurate.build.ConfigurateExtension

plugins {
    id 'eclipse'
    id 'com.diffplug.spotless'
}

def projectName = "configurate"

def configurateExt = project.extensions.create(projectName, ConfigurateExtension, dependencies)

def catalogs = extensions.findByType(VersionCatalogsExtension)
configurateExt.libs = catalogs?.named('libs')

spotless {
    format 'configs', {
        target '**/*.yaml', '**/*.yml', '**/*.xml', '**/*.json'

        def excludedTargets = ['.idea/**', 'vendor/**', 'build/**', '.gradle/**']
        project.subprojects { sub ->
            excludedTargets << sub.rootDir.relativePath(sub.projectDir) + '/**'
        }
        targetExclude excludedTargets

        endWithNewline()
        trimTrailingWhitespace()
    }
}

eclipse {
    project {
        if (!project.name.startsWith(projectName)) {
            name = "${projectName}-${project.name}"
        }
    }
}
