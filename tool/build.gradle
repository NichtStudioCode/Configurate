import io.gitlab.arturbosch.detekt.Detekt
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id 'application'
    alias(libs.plugins.kotlin)
    id 'org.spongepowered.configurate.build.component'
    alias(libs.plugins.detekt)
}

description = "CLI tool to inspect Configurate's view of files"

dependencies {
    // Configurate
    implementation projects.format.xml
    implementation projects.format.yaml
    implementation projects.format.gson
    implementation projects.format.hocon
    implementation projects.extra.extraKotlin

    // Libraries
    // implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation libs.clikt
    implementation libs.jansi
}

tasks.withType(KotlinCompile).configureEach {
    kotlinOptions.jvmTarget = "1.8"
    kotlinOptions.freeCompilerArgs = ["-Xnew-inference"]
}

tasks.jar.configure {
    manifest.attributes "Specification-Version": project.version,
    "Implementation-Version": project.version
}

tasks.distTar.configure {
    compression = Compression.GZIP
    archiveExtension = 'tar.gz'
}

tasks.withType(Detekt).configureEach {
    config.setFrom(rootProject.file(".detekt/detekt.yml"))
}

application {
    mainClass = "org.spongepowered.configurate.tool.ToolKt"
}
